/*
       Licensed to the Apache Software Foundation (ASF) under one
       or more contributor license agreements.  See the NOTICE file
       distributed with this work for additional information
       regarding copyright ownership.  The ASF licenses this file
       to you under the Apache License, Version 2.0 (the
       "License"); you may not use this file except in compliance
       with the License.  You may obtain a copy of the License at

         http://www.apache.org/licenses/LICENSE-2.0

       Unless required by applicable law or agreed to in writing,
       software distributed under the License is distributed on an
       "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
       KIND, either express or implied.  See the License for the
       specific language governing permissions and limitations
       under the License.
*/

// Set defaults before project's build-extras.gradle
if (!project.hasProperty('xwalkVersion')) {
    ext.xwalkVersion = cdvHelpers.getConfigPreference("xwalkVersion", "23+")
}

if (!project.hasProperty('xwalkCommandLine')) {
    ext.xwalkCommandLine = cdvHelpers.getConfigPreference("xwalkCommandLine", "")
}
// Apply values after project's build-extras.gradle
cdvPluginPostBuildExtras.add({
    repositories {
      maven {
        url 'https://download.01.org/crosswalk/releases/crosswalk/android/maven2'
      }
    }

    dependencies {
        implementation "org.xwalk:xwalk_core_library:" + xwalkVersion
    }

    if (file('assets/xwalk-command-line').exists()) {
        println('Not writing assets/xwalk-command-line since file already exists.')
        return
    }
    android.applicationVariants.all { variant ->
        def variantName = variant.name.capitalize()
        def mergeTask = tasks["merge${variantName}Assets"]
        def processTask = tasks["process${variantName}Resources"]
        def outFile = new File (mergeTask.outputDir, "xwalk-command-line")
        def newTask = project.task("createXwalkCommandLineFile${variantName}") << {
            mergeTask.outputDir.mkdirs()
            outFile.write("xwalk ${xwalkCommandLine}\n")
        }
        newTask.dependsOn(mergeTask)
        processTask.dependsOn(newTask)
    }
})
